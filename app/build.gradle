apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.codemate.blogreader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors{
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.1.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // test dependencies
    testImplementation jUnit
    testImplementation mockito
    testImplementation hamcrest

    // Android Testing Support Library's runner and rules
    androidTestImplementation supportTest.runner
    androidTestImplementation supportTest.rules

    // Espresso UI Testing dependencies.
    androidTestImplementation (supportTest.espressoCore) {
        espressoExcludes.each { exclude module : "$it" }
    }

    androidTestImplementation (supportTest.espressoContrib) {
        espressoExcludes.each { exclude module : "$it" }
    }

    androidTestImplementation (supportTest.espressoIntents) {
        espressoExcludes.each { exclude module : "$it" }
    }

    // app dependencies
    implementation support.appcompat
    implementation support.design
    implementation support.recyclerview
    implementation support.cardview

    annotationProcessor daggerCompiler
    implementation dagger
    compileOnly jsr250

    implementation retrofit
    implementation rxJava
    implementation rxAndroid
}